// the grammar of worm
// using the pest parser it is defined here to be turned into a rust structure in parser.rs
int = { ("+" | "-")? ~ ASCII_DIGIT+ }
float = @{ int ~ ("." ~ ASCII_DIGIT+) }

operation = _{ add | subtract | multiply | divide | power }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }
    power    = { "^" }
    modulus  = { "%" }

expr = { term ~ (operation ~ term)* }
term = _{float | int | char | func_call | identifier | string | hash_obj | array_empty | "(" ~ expr ~ ")" }
identifier = { var_name ~ (("." ~ var_name) | ("[" ~ expr ~ "]"))* }

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
WHITESPACE = _{ " "+ | "\t" | NEWLINE+}
reserved = {"print" | "if" | "else" | "while" | "true" | "false" | " " | "}" | "{" | ";" | ":"}

var_name = @{!reserved ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")*}

any = {"any"}
array_inst = {(vint | vfloat | vchar | vstring | hmap | structure) ~ "[]"}
array_dec = {array_inst}
piped = {"|" ~ var_name ~ "|"}
array_initial = {"[" ~ piped? ~ expr ~ ";" ~ expr ~ "]"}
array_definition = {array_inst ~ var_name ~ "=" ~ (array_initial) }
array_val = {"[" ~ list? ~ "]"}
array_empty =  {"[" ~ var_type ~ "]"}

structure_items = {(var_name ~ ":" ~ var_type) ~ ("," ~ (var_name ~ ":" ~ var_type))*}
structure_def = {"struct" ~ var_name ~ "{" ~ structure_items ~ ","? ~ "}"}

var_type = {array_inst | vint | vfloat | vstring | vbool | vchar | hmap | structure}
    vint = {"int"}
    vfloat = {"float"}
    vstring = {"string"}
    vbool = {"bool"}
    vchar = {"char"}
    hmap = {"map" ~ "<" ~ var_type ~ "," ~ var_type ~ ">"}
    structure = {"struct" ~ "<" ~ var_name ~ ">"}

hash_obj = {"{" ~ var_type ~ ":" ~ var_type ~ "}"}
string = ${ "\"" ~ inner ~ "\"" }
inner = @{ chars* }
chars = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
char = ${"\'" ~ chars ~ "\'"}

boolops = _{eq | geq | leq | neq | lt | gt}
    eq = {"=="}
    geq = {">="}
    leq = {"<="}
    neq = {"!="}
    lt = {"<"}
    gt = {">"}

bool_joiners = _{and | or}
    and = {"&"}
    or = {"|"}

not = _{"!"}
exprp = _{"("? ~ expr ~ ")"?}
boolexp = {exprp ~ boolops ~ exprp}
boolconst = _{tru | fal}
    tru = {"true"}
    fal = {"false"}

boolexpr = { "("? ~(boolterm | boolnot) ~ ")"? ~ (bool_joiners ~ (boolterm | boolnot))* ~ ")"?}
boolterm = {boolconst | boolexp | "(" ~ boolexp ~ ")" }
boolnot = {not ~ (("(" ~ boolterm ~ ")") | boolterm) }

del = {"del" ~ "(" ~ var_name ~ ")"}
skip = {"skip"}
print = {"print" ~ "(" ~ expr ~ ")"}
static_print = {"static_print" ~ "(" ~ expr ~ ")"}
assert = {"assert(" ~ boolexpr}
builtin = {del | print | static_print | assert}

body = _{statement+}
ifstm = {if_stm ~ elseif_stm* ~ else_stm?}
if_stm = {"if" ~ boolexpr ~ "{" ~ body+ ~ "}"}
elseif_stm = {"else if" ~ boolexpr ~ "{" ~ body ~ "}"}
else_stm = {"else" ~ "{" ~ body ~ "}"}

whilestm = {"while" ~ boolexpr ~ "{" ~ body+ ~ "}"}
parse_error = {ASCII_ALPHANUMERIC*}

dec = {var_type ~ var_name}
params = {dec ~ ("," ~ dec)*}
return_stm = {"return" ~ expr ~ ";"}
func_def = {"fn" ~ var_name ~ "(" ~ params? ~ ")" ~ "->" ~ var_type ~ "{" ~ body ~ "}"}

import_stm = {"import" ~ string ~ ";"}

item = _{int | float | var_type}
list = {expr ~ ("," ~ expr)*}
func_call = {var_name ~ "(" ~ list? ~ ")"}

assignment = {((var_type ~ var_name) | identifier) ~ "=" ~ (expr | string | array_initial)}
statement = _{((builtin | skip | array_definition |  assignment) ~ ";") | (return_stm | func_def | ifstm | whilestm) }
program = _{ SOI ~ (import_stm | structure_def |  func_def)* ~ EOI }
