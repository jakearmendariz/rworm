num = @{ int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)? }
    int = { ("+" | "-")? ~ ASCII_DIGIT+ }

operation = _{ add | subtract | multiply | divide | power }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }
    power    = { "^" }

expr = { term ~ (operation ~ term)* }
term = _{num | var_name | "(" ~ expr ~ ")" }

COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
WHITESPACE = _{ " "+ | "\t" | NEWLINE+}
reserved = {"print" | "if" | "else" | "while" | "true" | "false"}

var_name = {!reserved ~ ('a'..'z')+}
var_type = {vint | vfloat | vstring | vbool}
    vint = {"int"}
    vfloat = {"float"}
    vstring = {"string"}
    vbool = {"bool"}

string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

bool = _{tru | fal}
    tru = {"true"}
    fal = {"false"}

boolops = _{eq | geq | leq | neq}
    eq = {"=="}
    geq = {">="}
    leq = {"<="}
    neq = {"!="}
    lt = {"<"}
    gt = {">"}
boolexp = {(expr ~ boolops ~ expr) | bool}

body = {statement+}
ifstm = {"if" ~ boolexp ~ "{" ~ body+ ~ "}"}
whilestm = {"while" ~ boolexp ~ "{" ~ body+ ~ "}"}

assignment = {var_type? ~ var_name ~ "=" ~ (expr | string)}
print = {"print" ~ var_name}
statement = _{(print | ifstm | whilestm |assignment) ~ ";"}
program = _{ SOI ~ statement* ~ EOI }